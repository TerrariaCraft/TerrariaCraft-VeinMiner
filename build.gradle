buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.

ext.configFile = file("build.txt")
configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse(prop)
    project.ext.hasConfig = { key ->
        return config.isSet(key);
    }
}


version = config.mod_version
group = "org.zeith.tcvm"
archivesBaseName = "TerrariaCraftVeinMiner-${config.minecraft_version}"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    // Terraria Craft requires 14.23.5.2847 or later versions, but they seem to cause crashes in workspace, so we're using 2847 anyway.
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    
    // Terraria Craft requires stable_39 mappings.
    mappings = "stable_39"

    replace '@VERSION@', config.mod_version
}

repositories {
    maven {
        name = "Zeitheron Maven"
        url = "https://maven.zeith.org"
    }
}

dependencies {
	// Adds Terraria Craft 12.5.32 (ALONGSIDE IT'S DEPENDENCIES) into workspace
    deobfCompile "org.zeith.terraria:TerrariaCraft-1.12.2:${config.tc_version}"
	
	// Optionally, you may add Terraria Craft Recipe Viewer to make your life easier.
	deobfCompile "org.zeith.terraria:TerrariaCraftRecipeViewer-1.12.2:5.2.8"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}


task signJar(type: SignJar, dependsOn: reobfJar) {
    onlyIf {
        System.env.MC_KS_PASS != null
    }
    keyStore = System.env.MC_KS_PATH
    alias = System.env.MC_KS_ALIAS
    storePass = System.env.MC_KS_PASS
    keyPass = System.env.MC_KS_PASS
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

build.dependsOn signJar
